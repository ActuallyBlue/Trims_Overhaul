execute unless score #.tr.ability_shortcuts blue.config matches 1..2 run return run tellraw @s [{"text":"- ","color":"dark_gray"},{"translate":"blue.tr.ability_shortcuts","fallback":"Ability Shortcuts","color":"white","hoverEvent":{"action":"show_text","contents":[{"translate":"blue.tr.ability_shortcuts_hover","fallback":"Additional ways to activate specific trim abilities (OFF, Command, Both, Drop)","color":"gray"},{"translate":"blue.tr.shortcut_description","fallback":"\n Command: Use most sneak abilities via %s.\n Drop: Dropped weapons instantly return and trigger drop abilities (unless sneaking).","color":"dark_gray","with":[{"text":"/trigger Trims_Menu.BLUE set 100","color":"#858585"}]}]},"hover_event":{"action":"show_text","value":[{"translate":"blue.tr.ability_shortcuts_hover","fallback":"Additional ways to activate specific trim abilities (OFF, Command, Both, Drop)","color":"gray"},{"translate":"blue.tr.shortcut_description","fallback":"\n Command: Use most sneak abilities via %s.\n Drop: Dropped weapons instantly return and trigger drop abilities (unless sneaking).","color":"dark_gray","with":[{"text":"/trigger Trims_Menu.BLUE set 100","color":"#858585"}]}]},"italic":true,"extra":[{"text":"-","color":"dark_gray"},{"translate":"blue.tr.Command","fallback":"Command","color":"white"}]},{"translate":"blue.tr.is_disabled","fallback":" is disabled on this server","color":"gray"}]
execute if score @s blue.tr.current_trim matches 6 unless score #.rib.projectile_cooldown blue.misc matches 1 if score #.rib.strict_projectile blue.config matches 1 unless score @s blue.tr.combat matches 200.. anchored eyes run function blue:tr/effects/rib/proj/spawn_fireball
execute if score @s blue.tr.current_trim matches 6 unless score #.rib.projectile_cooldown blue.misc matches 1 unless score #.rib.strict_projectile blue.config matches 1 anchored eyes run return run function blue:tr/effects/rib/proj/spawn_fireball
execute if score @s blue.tr.current_trim matches 11 unless score #.spire.guidance_cooldown blue.misc matches 1 unless predicate blue:shared/on_ground run return run function blue:tr/effects/spire/guidance
execute if score @s blue.tr.current_trim matches 12 unless score #.tide.weak_cooldown blue.misc matches 1 if score #.tide.stacks blue.misc matches 1.. if score #.temp blue.misc matches 1 run return run function blue:tr/effects/tide/emp/burst_checks
execute if score @s blue.tr.current_trim matches 13 if score #.vex.sneak blue.misc matches 0.. rotated ~ -10 run return run function blue:tr/effects/vex/lunge/start
execute if score @s blue.tr.current_trim matches 15 run effect give @s slow_falling 2 0 true