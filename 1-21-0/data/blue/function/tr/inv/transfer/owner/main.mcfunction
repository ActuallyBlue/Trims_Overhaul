playsound block.end_portal_frame.fill player @a ~ ~ ~ 1 0.5
execute if score #.tr.limit_owned_trims blue.config matches 1.. if entity @a[tag=blue.tr.transfer_target,advancements={blue:tr/tags={trim_limit=true}},limit=1] run return run tellraw @s [{"text":"- ","color":"dark_gray"},{"selector":"@a[tag=blue.tr.transfer_target]"}," ",{"translate":"blue.tr.trim_limit","fallback":"already has a trim"}," ",{"text":"â„¹","hoverEvent":{"action":"show_text","contents":{"translate":"blue.tr.setting_enabled","fallback":"The %s setting is enabled\n%s","color":"gray","with":[{"translate":"blue.tr.limit_owned_trims","fallback":"Limit Owned Trims","color":"white"},{"translate":"blue.tr.limit_owned_trims_hover","fallback":"Players can only own 1 trim at a time (OFF, ON)","color":"dark_gray","italic":true}]}},"hover_event":{"action":"show_text","value":{"translate":"blue.tr.setting_enabled","fallback":"The %s setting is enabled\n%s","color":"gray","with":[{"translate":"blue.tr.limit_owned_trims","fallback":"Limit Owned Trims","color":"white"},{"translate":"blue.tr.limit_owned_trims_hover","fallback":"Players can only own 1 trim at a time (OFF, ON)","color":"dark_gray","italic":true}]}}}]
$execute if entity @a[tag=blue.tr.transfer_target,advancements={blue:tr/blacklist={$(trim)=true}},limit=1] run return run tellraw @s [{"text":"- ","color":"dark_gray"},{"selector":"@a[tag=blue.tr.transfer_target]"}," ",{"translate":"blue.tr.transfer_blacklist","fallback":"has been blacklisted from getting","color":"gray"}," ",{"translate":"$(translate)","color":"$(color)"}]
$execute unless entity @s[tag=blue.tr.trim.$(trim)] store result score #.temp blue.misc run clear @s $(trim)_armor_trim_smithing_template 0
$execute unless entity @s[tag=blue.tr.trim.$(trim)] if score #.temp blue.misc matches 4.. as @a[tag=blue.tr.transfer_target,limit=1] run function blue:tr/inv/transfer/target/swap with storage blue:trims translations[{trim:$(trim)}]
$execute unless entity @s[tag=blue.tr.trim.$(trim)] unless entity @s[tag=blue.tr.$(trim)] run clear @s $(trim)_armor_trim_smithing_template
$execute if entity @s[tag=blue.tr.trim.$(trim)] in overworld as @a[tag=blue.tr.transfer_target,limit=1] run function blue:tr/inv/transfer/target/armor with storage blue:trims translations[{trim:$(trim)}]
$execute if entity @s[tag=blue.tr.trim.$(trim)] unless entity @s[tag=blue.tr.$(trim)] run function blue:tr/inv/transfer/owner/remove_trim
$execute if entity @s[tag=blue.tr.$(trim)] run return run function blue:tr/inv/transfer/owner/fail
$execute as @a[tag=blue.tr.transfer_target,limit=1] facing entity @s feet run function blue:tr/inv/transfer/raycast {trim:$(trim)}
data modify storage blue:trims logs.append.target set from storage blue:shared player_name
function blue:shared/get_name
$data modify storage blue:trims logs.append.trim set value {"translate":"$(translate)","color":"$(color)"}
function blue:tr/inv/transfer/log